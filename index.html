<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>woscilloscope</title>
    <style>
html { background: black; color: #ddd; }
body { width:800px;margin:auto; }
audio { width: 800px; }
h2 { text-align: center; margin: 5px; }
ul { text-align: center; font-size: 20px; margin: 5px; }
li { display: inline; margin: 0 10px 0 10px; }
a { color: #2d2; }
.nogl { display: none; }
noscript, .nogl { position: absolute; width: 800px; padding-top: 200px;
text-align: center; font-size: 32px; }
footer { text-align: center; font-size: 18px; }
    </style>
    <script src="dist/demo.js" data-fix-firefox=true> </script>
    <script>
// I refuse to replace `let` with `var`s
if (!('audioCtx' in window) && navigator.userAgent.match(/Firefox/)) {
    document.addEventListener('DOMContentLoaded', function() {
        console.log('Trying to re-evaluate scripts with js ver1.7 attribute')
        var scripts = document.querySelectorAll('script[data-fix-firefox]');
        [].forEach.call(scripts, function(s) {
           document.head.removeChild(s);
           var copy = document.createElement('script');
           if (s.src) {
               copy.src = s.src;
           } else {
               copy.innerHTML = s.innerHTML;
           }
           copy.setAttribute('type', 'application/javascript;version=1.7');
           document.head.appendChild(copy);
        });
    });
}
    </script>
    <script language="x-shader/x-vertex" id="vsLine">
precision highp float;
#define EPS 1E-6
uniform float uInvert;
uniform float uSize;
attribute vec2 aStart, aEnd;
attribute float aIdx;
varying vec4 uvl;
varying float vLen;
void main () {
    float tang;
    vec2 current;
    // All points in quad contain the same data:
    // segment start point and segment end point.
    // We determine point position from it's index.
    float idx = mod(aIdx,4.0);
    if (idx >= 2.0) {
        current = aEnd;
        tang = 1.0;
    } else {
        current = aStart;
        tang = -1.0;
    }
    float side = (mod(idx, 2.0)-0.5)*2.0;
    uvl.xy = vec2(tang, side);
    uvl.w = floor(aIdx / 4.0 + 0.5);

    vec2 dir = aEnd-aStart;
    uvl.z = length(dir);
    if (uvl.z > EPS) {
        dir = dir / uvl.z;
    } else {
    // If the segment is too short draw a square;
        dir = vec2(1.0, 0.0);
    }
    vec2 norm = vec2(-dir.y, dir.x);
    gl_Position = vec4((current+(tang*dir+norm*side)*uSize)*uInvert,0.0,1.0);
    //gl_PointSize = 20.0;
}
    </script>
    <script language="x-shader/x-fragment" id="fsLine">
precision highp float;
#define EPS 1E-6
#define TAU 6.283185307179586
#define TAUR 2.5066282746310002
#define SQRT2 1.4142135623730951
uniform float uSize;
uniform float uIntensity;
precision highp float;
varying vec4 uvl;
float gaussian(float x, float sigma) {
    return exp(-(x * x) / (2.0 * sigma * sigma)) / (TAUR * sigma);
}

float erf(float x) {
    float s = sign(x), a = abs(x);
    x = 1.0 + (0.278393 + (0.230389 + (0.000972 + 0.078108 * a) * a) * a) * a;
    x *= x;
    return s - s / (x * x);
}
void main (void)
{
    float len = uvl.z;
    vec2 xy = vec2((len/2.0+uSize)*uvl.x+len/2.0, uSize*uvl.y);
    float alpha;

    float sigma = uSize/4.0;
    if (len < EPS) {
    // If the beam segment is too short, just calculate intensity at the position.
        alpha = exp(-pow(length(xy),2.0)/(2.0*sigma*sigma))/2.0/sqrt(uSize);
    } else {
    // Otherwise, use analytical integral for accumulated intensity.
        alpha = erf((len-xy.x)/SQRT2/sigma) + erf(xy.x/SQRT2/sigma);
        alpha *= exp(-xy.y*xy.y/(2.0*sigma*sigma))/2.0/len*uSize;
    }
    float afterglow = smoothstep(0.0, 0.33, uvl.w/2048.0);
    alpha *= afterglow * uIntensity;
    gl_FragColor = vec4(1./32., 1.0, 1./32., alpha);
}
</script>
<script type="x-shader/x-vertex" id="vsOutput">
precision highp float;
uniform float uSize;
attribute vec2 aPos, aST;
varying vec2 vTexCoord;
void main (void) {
    gl_Position = vec4(aPos, 1, 1);
    vTexCoord = aST*uSize/1024.0;
}
</script>
<script type="x-shader/x-fragment" id="fsOutput">
precision highp float;
uniform sampler2D uTexture;
uniform float uAlpha;
varying vec2 vTexCoord;
void main (void) {
    vec4 color = texture2D(uTexture, vTexCoord);
    color.a = uAlpha;
    gl_FragColor = color;
}
</script>
<script type="x-shader/x-vertex" id="vsBlurTranspose">
precision highp float;
uniform float uSize;
attribute vec2 aPos, aST;
varying vec2 vTexCoord;
void main (void) {
    gl_Position = vec4(aPos.y, aPos.x, 1, 1);
    vTexCoord = aST*uSize/1024.0;
}
</script>
<script type="x-shader/x-fragment" id="fsBlurTranspose">
precision highp float;
uniform sampler2D uTexture;
uniform float uSize;
varying vec2 vTexCoord;
void main (void) {
    float point = uSize/1024.0/1024.0*2.0;
    vec4 color = texture2D(uTexture, vTexCoord);
    float sum = 0.0;
    sum += texture2D(uTexture, vec2(vTexCoord.x - point*4.0, vTexCoord.y)).g * (1.0/25.0);
    sum += texture2D(uTexture, vec2(vTexCoord.x - point*3.0, vTexCoord.y)).g * (2.0/25.0);
    sum += texture2D(uTexture, vec2(vTexCoord.x - point*2.0, vTexCoord.y)).g * (3.0/25.0);
    sum += texture2D(uTexture, vec2(vTexCoord.x - point*1.0, vTexCoord.y)).g * (4.0/25.0);
    sum += texture2D(uTexture, vec2(vTexCoord.x            , vTexCoord.y)).g * (5.0/25.0);
    sum += texture2D(uTexture, vec2(vTexCoord.x + point*1.0, vTexCoord.y)).g * (4.0/25.0);
    sum += texture2D(uTexture, vec2(vTexCoord.x + point*2.0, vTexCoord.y)).g * (3.0/25.0);
    sum += texture2D(uTexture, vec2(vTexCoord.x + point*3.0, vTexCoord.y)).g * (2.0/25.0);
    sum += texture2D(uTexture, vec2(vTexCoord.x + point*4.0, vTexCoord.y)).g * (1.0/25.0);
    gl_FragColor = vec4(0.0, sum, 0.0, 1.0);
}
</script>
<script type="x-shader/x-vertex" id="vsProgress">
precision highp float;
attribute vec2 aPos;
void main (void) {
    gl_Position = vec4(aPos, 1, 1);
}
</script>
<script type="x-shader/x-fragment" id="fsProgress">
precision highp float;
uniform float uProgress;
float rect(vec2 p, vec2 s) {
    return max(abs(p.x)-s.x,abs(p.y)-s.y);
}
void main (void) {
    float p = clamp(uProgress, 0.0, 1.0);
    float hw = 300.0;
    vec2 size = vec2(800.0, 800.0);
    vec2 c = size / 2.0;
    vec2 uv = gl_FragCoord.xy-c;
    float result = min(rect(uv,vec2(hw+5.,25.)),-rect(uv,vec2(hw+10.,30.)));
    result = max(result,-rect(uv-vec2(hw*(p-1.0),0.0),vec2(hw*p, 20.0)));
    gl_FragColor = vec4(vec3(0.1, 1.0, 0.1) * clamp(result, 0.0, 1.0), 1.0);
}
</script>
</head>
<body>
<noscript>Did you expect this thing to work without js enabled?</noscript>
<div id="nogl" class="nogl">It seem you don't have a WebGL-capable web browser :(</div>
<canvas id="c" width=800 height=800></canvas><br>
<audio controls id="htmlAudio"></audio><br>
<h2 id="songInfo">song info goes here</h2>
<ul id="playList"></ul>
<footer><a href="https://github.com/m1el/woscope">woscope</a> by m1el</footer>
</body>
</html>
